plugins {
    id "com.jfrog.bintray" version "1.7.3"
    id "com.github.dcendents.android-maven" version "1.5"
}
apply plugin: 'com.android.library'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 17
        versionName "3.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

group = 'net.idik'
version = '2.1.2'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

def siteUrl = 'https://github.com/MEiDIK/SlimAdapter'
def gitUrl = 'https://github.com/MEiDIK/SlimAdapter.git'

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    pkg {
        repo = 'SlimAdapter'
        name = 'SlimAdapter'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ['MIT']
        userOrg = 'idik-net'
        publish = true

        version {
            name = '2.1.2'
            desc = 'New Feature: SlimAdapterEx'
            released = new Date()
            vcsTag = 'v2.1.2'
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
    configurations = ['archives']
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    implementation 'androidx.recyclerview:recyclerview:1.0.0-rc02'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}
repositories {
    mavenCentral()
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'SlimAdapter'
                description 'A slim & clean & typeable Adapter without # VIEWHOLDER'
                url siteUrl
                licenses {
                    license {
                        name 'MIT'
                        url 'https://github.com/MEiDIK/SlimAdapter/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id 'iDIK'
                        name 'iDIK'
                        email 'me@idik.net'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
//    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}